configfile: "../config.yaml"

datasrc = {
    'focal' : 'TableS2D.xlsx',
    'aneup' : 'dummy',
    'purity' : 'dummy',
    'immune' : '1-s2.0-S1074761318301213-mmc2.xlsx'
}

rule all:
    input:
        "ng.RData",
        expand("{obj}.RData", obj=datasrc.keys()),
        expand("expr_{correction}/{cohort}.RData", correction=config['correction'],
                cohort=config['cohorts'])

rule ng:
    input:
        rscript = "ng.r",
        annot = "tf_annot.RData",
        binding = "tf_binding.RData",
        expr = expand("expr_{correction}/{cohort}.RData",
                correction=config['correction'], cohort=config['cohorts'])
    output:
        outfile = "ng.RData"
    shell:
        "Rscript {input.rscript}"
            " --annot {input.annot}"
            " --binding {input.binding}"
            " --outfile {output.outfile}"
            " {input.expr}"

rule tf:
    input:
        rscript = "tf.r"
    output:
        annot = "tf_annot.RData",
        binding = "tf_binding.RData"
    shell:
        "Rscript {input.rscript}"
            " --annot {output.annot}"
            " --binding {output.binding}"

rule expr:
    input:
        rscript = "expr.r"
    output:
        data = "expr_{correction}/{cohort}.RData"
    resources:
        mem = 10240,
        walltime = 120
    shell:
        "Rscript {input.rscript}"
            " --cohort {wildcards.cohort}"
            " --correction {wildcards.correction}"
            " --outfile {output.data}"

rule obj:
    input:
        rscript = "{obj}.r",
        data = lambda wc: datasrc[wc.obj]
    output:
        data = "{obj}.RData"
    resources:
        mem = 4096,
        time = 48*60
#    wildcard_constraints:
#        obj = "^(^expr)"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.data}"
            " --outfile {output.data}"

rule amps:
    output:
        "TableS2D.xlsx"
    shell:
        "wget https://www.cancerrxgene.org/gdsc1000/GDSC1000_WebResources//Data/suppData/TableS2D.xlsx"

rule immune:
    output:
        "1-s2.0-S1074761318301213-mmc2.xlsx"
    shell:
        "wget https://ars.els-cdn.com/content/image/1-s2.0-S1074761318301213-mmc2.xlsx"
