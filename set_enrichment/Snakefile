configfile: "../config.yaml"

subworkflow data:
    workdir: "../data"
    snakefile: "../data/Snakefile"

subworkflow net:
    workdir: "../networks"
    snakefile: "../networks/Snakefile"

rule all:
    input:
        expand("plot_{region}.pdf", region=config['set_enrichment']),
        "TFbinding_enrichment.pdf"

rule TFbinding_enrichment:
    input:
        rscript = "TFbinding_enrichment.r",
        real = "../networks/TFbinding/naive/ACC.RData", # same for all
        ng = data("ng.RData"),
        net = net(expand("{method}/{correction}/{cohort}.RData",
                 method=[x for x in config['methods'] if x != "TFbinding"],
                 correction=config['correction'], cohort=config['cohorts']))
    output:
        outfile = "TFbinding_enrichment.RData",
        plotfile = "TFbinding_enrichment.pdf"
    shell:
        "Rscript {input.rscript}"
            " --real {input.real}"
            " --ng {input.ng}"
            " --outfile {output.outfile}"
            " --plotfile {output.plotfile}"
            " {input.net}"

rule plot:
    input:
        rscript = "plot_{region}.r",
        infile = "merge.RData"
    output:
        plotfile = "plot_{region}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.infile}"
            " --region {wildcards.region}"
            " --plotfile {output.plotfile}"

rule merge:
    input:
        rscript = "merge.r",
        infiles = expand("{region}_{method}/{correction}/{cohort}.RData",
                region=config['set_enrichment'], method=config['methods'],
                correction=config['correction'], cohort=config['cohorts'])
    output:
        outfile = "merge.RData"
    shell:
        "Rscript {input.rscript}"
            " --outfile {output.outfile}"
            " {input.infiles}"

rule fet:
    input:
        rscript = "check_set_enrichment.r",
        network = net("{method}/{correction}/{cohort}.RData"),
        ng = data("ng.RData"),
        sets = data("{region}.RData")
    output:
        result = "{region}_{method}/{correction}/{cohort}.RData"
    shell:
        "Rscript {input.rscript}"
            " --config ../config.yaml"
            " --cohort {wildcards.cohort}"
            " --ng {input.ng}"
            " --sets {input.sets}"
            " --method {wildcards.method}"
            " --network {input.network}"
            " --outfile {output.result}"
