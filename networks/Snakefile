configfile: "../config.yaml"

subworkflow data:
    workdir: "../data"
    snakefile: "../data/Snakefile"

mem = {
    'TFbinding' : 1024,
    'aracne' : 10240,
    'genenet' : 102400,
    'GeneNet.wrap' : 102400
}

cores = {
    'Genie3' : 10,
    'Genie3+TF' : 10,
    'Tigress' : 10,
    'Tigress+TF': 10
}

rule all:
    input:
        expand("{method}/{correction}/{cohort}.RData", method=config['methods'],
                cohort=config['cohorts'], correction=config['correction'])

rule network:
    input:
        rscript = "network.r",
        expr = data("expr_{correction}/{cohort}.RData"),
        tf_annot = data("tf_annot.RData"),
        tf_binding = data("tf_binding.RData")
    output:
        data = "{method}/{correction}/{cohort}.RData"
    resources:
        mem = lambda wc: mem[wc.method] if wc.method in mem.keys() else 40960,
        walltime = 7*24*60 # 3 days max for 'short' on peregrine
    threads:
        lambda wc: cores[wc.method] if wc.method in cores.keys() else 1,
    shell:
        "Rscript {input.rscript}"
            " --cohort {wildcards.cohort}"
            " --expr {input.expr}"
            " --method {wildcards.method}"
            " --tf_annot {input.tf_annot}"
            " --tf_binding {input.tf_binding}"
            " --outfile {output.data}"
            " --cores {threads}"
